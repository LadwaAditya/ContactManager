apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    dataBinding {
        enabled true
    }
    defaultConfig {
        applicationId "com.ladwa.aditya.gojek"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "GOJEK_API_URL", "\"${GoJekApiUrl}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    configurations.all {
        resolutionStrategy {
            force 'com.squareup.okio:okio:1.11.0'
            force "com.squareup.okhttp3:okhttp:3.5.0"
        }
    }

    retrolambda {
        jvmArgs '-noverify'
        incremental true
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '25.1.1'
    final RETROFIT_VERSION = '2.1.0'
    final OKHTTP_VERSION = '3.5.0'
    final DAGGER_VERSION = '2.8'
    final HAMCREST_VERSION = '1.3'
    final ESPRESSO_VERSION = '2.2.2'
    final RUNNER_VERSION = '0.5'
    final BUTTERKNIFE_VERSION = '8.5.1'
    final MOCKITO_VERSION = '2.6.8'
    final RESTMOCK_VERSION = '0.2.0'
    final TIMBER_VERSION = '4.5.1'
    final STORIO_VERSION = "1.12.2"

    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    def jUnit = "junit:junit:4.12"
    def mockito = "org.mockito:mockito-core:$MOCKITO_VERSION"

    // App Dependencies
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"


    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"


    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
    compile 'com.hannesdorfmann.smoothprogressbar:library:1.0.0'
    compile('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }


    compile "io.reactivex.rxjava2:rxjava:2.0.5"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"

    //Icons
    compile 'com.mikepenz:iconics-core:2.8.2@aar'
    compile 'com.mikepenz:community-material-typeface:1.7.22.1@aar'

    //StorIO
    compile "com.pushtorefresh.storio:sqlite:$STORIO_VERSION"
    compile "com.pushtorefresh.storio:content-resolver:$STORIO_VERSION"
    compile "com.pushtorefresh.storio:sqlite-annotations:$STORIO_VERSION"
    compile "com.pushtorefresh.storio:content-resolver-annotations:$STORIO_VERSION"
    annotationProcessor "com.pushtorefresh.storio:sqlite-annotations-processor:$STORIO_VERSION"
    annotationProcessor "com.pushtorefresh.storio:content-resolver-annotations-processor:$STORIO_VERSION"

    //RxPermission for easy runtime permission management
    compile 'com.devdoo.rxpermissions:lib:1.0.2'

    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"
    compile "com.facebook.stetho:stetho-okhttp3:1.4.2"
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2




    // Instrumentation test dependencies
    androidTestCompile jUnit
    androidTestCompile mockito
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }


    androidTestCompile "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestCompile "com.android.support.test:runner:$RUNNER_VERSION"
    androidTestCompile "com.android.support.test:rules:$RUNNER_VERSION"
    androidTestCompile "org.mockito:mockito-android:$MOCKITO_VERSION"
    androidTestCompile "com.github.andrzejchm.RESTMock:android:$RESTMOCK_VERSION"


    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Unit tests dependencies
    testCompile jUnit
    testCompile mockito
    testCompile "org.hamcrest:hamcrest-core:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-integration:$HAMCREST_VERSION"
    testCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    testCompile "com.github.andrzejchm.RESTMock:android:$RESTMOCK_VERSION"
    testCompile "org.easymock:easymock:3.4"
    testCompile "com.squareup.assertj:assertj-android:1.1.1"        // AssertJ
    testCompile "org.powermock:powermock-core:1.6.6"                // PowerMock
    testCompile "org.powermock:powermock-module-junit4:1.6.6"
    testCompile "org.powermock:powermock-api-easymock:1.6.6"
    // PowerMock + EasyMock extension

    // APT dependencies
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"
    annotationProcessor daggerCompiler
    testAnnotationProcessor daggerCompiler
    androidTestAnnotationProcessor daggerCompiler

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.5.0'

    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}
